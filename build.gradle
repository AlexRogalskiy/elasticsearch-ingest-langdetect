buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath "org.elasticsearch.gradle:build-tools:5.6.4"
  }
}

plugins {
  id 'net.researchgate.release' version '2.4.0'
}

group = 'de.spinscale.elasticsearch.plugin'
version = '5.6.4.1-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'elasticsearch.esplugin'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

// this is temporal and will be fixed in 6.0
ext.projectSubstitutions = [:]

// license of this project
licenseFile = rootProject.file('LICENSE.txt')
// copyright notices
noticeFile = rootProject.file('NOTICE.txt')

esplugin {
  name 'ingest-langdetect'
  description 'Ingest processor doing language detection for fields'
  classname 'org.elasticsearch.plugin.ingest.langdetect.IngestLangDetectPlugin'
  // license of the plugin, may be different than the above license
  licenseFile rootProject.file('LICENSE.txt')
  // copyright notices, may be different than the above notice
  noticeFile rootProject.file('NOTICE.txt')
}

// In this section you declare the dependencies for your production and test code
dependencies {
  compile 'com.youcruit.com.cybozu.labs:langdetect:1.1.2-20151117'
  compile 'net.arnx:jsonic:1.3.10'
}

dependencyLicenses.enabled = false
thirdPartyAudit.enabled = false

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Elasticsearch Ingest Language Detection Plugin'
        packaging 'zip'
	    artifactId 'ingest-langdetect'
        description 'Ingest processor doing language detection for fields'
        url 'https://github.com/spinscale/elasticsearch-ingest-langdetect'

        scm {
          connection 'scm:git:https://github.com/spinscale/elasticsearch-ingest-langdetect'
          developerConnection 'scm:git:git@github.com:spinscale/elasticsearch-ingest-langdetect.git'
          url 'https://github.com/spinscale/elasticsearch-ingest-langdetect'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'spinscale'
            name 'Alexander Reelsen'
            email 'alr@spinscale.de'
          }
        }
      }
    }
  }
}
